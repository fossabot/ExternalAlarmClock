// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.github.rholder:gradle-one-jar:1.0.4'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.2.2'
 
        //Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

configure(javaProjects()) {
    apply plugin: 'eclipse'
    apply plugin: "info.solidsoft.pitest"

    eclipse {
        classpath {
            //customizing the classes output directory:
            defaultOutputDir = file('build-eclipse')

            //default settings for downloading sources and Javadoc:
            downloadSources = true
            downloadJavadoc = false
        }
    }

    pitest {
        //targetClasses = ['com.*']  //by default "${project.group}.*"
        pitestVersion = "1.2.4" //not needed when a default PIT version should be used
        threads = 4
        outputFormats = ['XML', 'HTML']
        failWhenNoMutations = false
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.11.0'
    }
}

def javaProjects() {
    subprojects - rootProject() - appProjects()
}

def rootProject() {
    [project]
}

def appProjects() {
    subprojects.findAll { it.name.endsWith('app') }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
