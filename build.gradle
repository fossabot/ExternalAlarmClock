// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
        classpath "org.owasp:dependency-check-gradle:3.3.4" 
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.0"
        classpath "com.bmuschko:gradle-docker-plugin:4.0.4"
	classpath "net.saliman:gradle-cobertura-plugin:2.3.1"
        classpath "com.github.kt3k.coveralls:coveralls-gradle-plugin:2.8.2"
        //Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: "base"
apply plugin: "org.owasp.dependencycheck"
apply plugin: "org.sonarqube"
apply plugin: "cobertura"
apply plugin: "com.github.kt3k.coveralls"
sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
        property "sonar.pitest.mode", "reuseReport"
    }
}
cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report

allprojects {
    repositories {
        jcenter()
    }
}

configure(javaProjects()) {
    apply plugin: 'eclipse'
    apply plugin: "info.solidsoft.pitest"
    apply plugin: "jacoco"

    eclipse {
        classpath {
            //customizing the classes output directory:
            defaultOutputDir = file('build-eclipse')

            //default settings for downloading sources and Javadoc:
            downloadSources = true
            downloadJavadoc = false
        }
    }

    pitest {
        targetClasses = ['com.*']
        pitestVersion = "1.4.3" //not needed when a default PIT version should be used
        threads = 4
        outputFormats = ['XML', 'HTML']
        failWhenNoMutations = false
    }

    dependencies {
        testCompile 'junit:junit:4.12'
	testCompile 'com.openpojo:openpojo:0.8.10'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
    }
}

def javaProjects() {
    subprojects - rootProject() - appProjects()
}

def rootProject() {
    [project]
}

def appProjects() {
    subprojects.findAll { it.name.endsWith('app') }
}

apply plugin: "com.github.ben-manes.versions"
dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

check.dependsOn dependencyUpdates
check.dependsOn dependencyCheckAggregate
check.dependsOn cobertura
check.dependsOn coveralls
coveralls.dependsOn cobertura
